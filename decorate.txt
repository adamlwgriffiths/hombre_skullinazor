ACTOR HombreDoom: DoomPlayer {
	Player.WeaponSlot 1, Fist, HombreSkullinazor
	Player.WeaponSlot 2, Pistol
	Player.WeaponSlot 3, Shotgun, SuperShotgun
	Player.WeaponSlot 4, Chaingun
	Player.WeaponSlot 5, RocketLauncher
	Player.WeaponSlot 6, PlasmaRifle
	Player.WeaponSlot 7, BFG9000
}

ACTOR HombreSkullinazor: Weapon Replaces Chainsaw 20000 {
	Weapon.SelectionOrder 50
	Weapon.AmmoUse 5
	Weapon.AmmoGive 100
	Weapon.AmmoType "Clip"
	
	Inventory.PickupSound "weapon/getskull"
	Inventory.PickupMessage "Got the Homebre Skullinazor"

	States {
		Spawn:
		SKLL A -1
		Stop
		
		Select:
		HSKL A 1 A_Raise
		Loop
		
		Deselect:
		HSKL A 1 A_Lower
		Loop
		
		Ready:
		HSKL A 1 A_WeaponReady
		Loop
		
		Fire:
		HSKL BCD 4
		Goto SkullinazorFire
		
		SkullinazorFire:
		HSKL E 0 A_PlaySound("weapon/fireskull", CHAN_WEAPON, 0.7)
		HSKL E 0 A_Recoil(4)
		HSKL E 5 A_FireCustomMissile("HombreSkullinazorMissile", 0, 2, 0, -8)
		HSKL D 5
		HSKL D 0 A_Refire("SkullinazorFire")
		Goto Ready
	}
}

ACTOR HombreSkullinazorMissile: Actor 20001 {
	+RANDOMIZE
	
	Decal "Scorch"
	Projectile
	
	Radius 6
	Height 8
	Speed 20
	Damage 15
	
	DeathSound "weapons/plasmax"
	
	States {
		Spawn:
		SKLM AB 4 Bright
		Loop
		
		Death:
		SKLM CDE 4 Bright
		Stop
	}
}
